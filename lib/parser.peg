{
    var T = require('./ast_types');

    function mk(thing, opts) {
        opts.type = thing;
        return opts;
    }

    function mkMemberAccess(variable, members) {
        var prev = mk(T.VARIABLE, {name: variable});
        members.forEach(function(m) {
            if (m.t === 'static') {
                prev = mk(T.STATIC_MEMBER, {
                    subject: prev,
                    member: m.name
                });
            } else {
                throw new Error("unknown member type!");
            }
        });
        return prev;
    }
}

//
// Script

Script
    = __ leader:Statements sections:NamedSection* {
        if (leader.length) {
            leader = mk(T.SECTION, { name: 'actions', body: leader });
            sections.unshift(leader);
        }
        return sections;
    }

NamedSection
    = label:SectionLabel body:Statements {
        return mk(T.SECTION, { name: label, body: body });
    }

SectionLabel
    = name:SectionLabelName ':' TERM {
        return name;
    }

SectionLabelName
    = $ 'inputs'
    / $ 'actions'

Statements
    = __ statements:(Statement)* {
        return statements;
    }

Statement
    = IfStatement
    / Directive

IfStatement
    = IfKeyword _ cond:ScriptExpression _ ThenKeyword __ body:Statements alt:ElseClause? EndKeyword TERM {
        return mk(T.IF, { condition: cond, consequent: body, alternate: alt || [] });
    }

ElseClause
    = ElseKeyword __ body:Statements {
        return body;
    }

Directive
    = name:ident _ args:DirectiveArgs? TERM {
        return mk(T.DIRECTIVE, {
            name: name,
            args: args || { positional: [], named: {} }
        });
    }

DirectiveArgs
    = n:DirectiveNamedArgs {
        return { positional: [], named: n };
    }
    / p:DirectivePositionalArgs n:(',' __ n:DirectiveNamedArgs)? {
        return { positional: p, named: n ? n[2] : {} };
    }
    
DirectivePositionalArgs
    = head:DirectivePositionalArg tail:(',' __ DirectivePositionalArg)* {
        var out = [head];
        for (var i = 0; i < tail.length; ++i) {
            out.push(tail[i][2]);
        }
        return out;
    }

DirectivePositionalArg
    = arg:ScriptExpression { return arg; }

DirectiveNamedArgs
    = head:DirectiveNamedArgPair tail:(',' __ DirectiveNamedArgPair)* {
        var out = {};
        out[head.key] = head.value;
        for (var i = 0; i < tail.length; ++i) {
            out[tail[i][2].key] = tail[i][2].value;
        }
        return out;
    }

DirectiveNamedArgPair
    = key:key _ value:ScriptExpression {
        return { key: key, value: value };
    }

//
// Expression Parsing

ScriptExpression
    = Pipeline
    / PartialPipeline
    / Atom

Pipeline
    = head:Atom items:(__ '|' __ FunctionCall)+ {
        return mk(T.PIPELINE, {
            initial: head,
            pipeline: mk(T.PARTIAL_PIPELINE, {
                list: items.map(function(i) { return i[3]; })
            })
        });
    }

PartialPipeline
    = '|' __ head:FunctionCall tail:(__ '|' __ FunctionCall)* {
        return mk(T.PARTIAL_PIPELINE, {
            list: [head].concat(tail.map(function(i) { return i[3]; }))
        });
    }

//
// Template Parsing

FileTemplate
    = FileChunk*

FileChunk
    = TemplateExpansion
    / TemplateCondition
    / TemplateLoop
    / Raw

TemplateExpansion
    = EXPANSION_START __ exp:ScriptExpression __ EXPANSION_END { return exp; }

TemplateCondition
    = cond:TemplateIfClause alt:TemplateElseClause? TemplateEnd {
        return mk(T.TEMPLATE_IF, {
            condition: cond[0],
            consequent: cond[1],
            alternate: alt || []
        });
    }

TemplateIfClause
    = '{%' _ IfKeyword _ exp:ScriptExpression _ '%}' chunks:FileTemplate {
        return [exp, chunks];
    }

TemplateElseClause
    = '{%' _ ElseKeyword _ '%}' chunks:FileTemplate {
        return chunks;
    }

TemplateLoop
    = loop:TemplateForeach TemplateEnd {
        return loop;
    }

TemplateForeach
    = '{%' _ ForeachKeyword _ k:(variable_name _ ',' _)? v:variable_name InKeyword _ exp:ScriptExpression _ '%}' chunks:FileTemplate {
        return mk(T.TEMPLATE_FOREACH, {
            key: k ? k[0] : null,
            value: v,
            subject: exp,
            body: chunks
        });
    }

TemplateEnd
    = '{%' _ EndKeyword _ '%}'

Raw
    = $( [^{] / '{' ![{%] )+

//
// Atoms

Atom
    = a:FunctionCall _  { return a; }
    / a:Number _        { return a; }
    / a:UserDefault _   { return a; }
    / a:Variable _      { return a; }
    / a:String _        { return a; }
    / a:Symbol _        { return a; }
    / a:Array _         { return a; }
    / a:Dictionary _    { return a; }

FunctionCall
    = name:ident '(' _ args:AtomList? ')' {
        return mk(T.CALL, {name: name, args: args || []});
    }

Number
    = n:Integer {
        return n;
    }

Integer
    = val:$( [1-9] [0-9]* ) { return parseInt(val, 10); }
    / $( '0'+ ) ![1-9] { return 0; }

UserDefault
    = '$' m:member+ {
        return mkMemberAccess('defaults', m);
    }

Variable
    = v:simple_variable m:member+ {
        return mkMemberAccess(v, m);
    }
    / v:simple_variable {
        return mk(T.VARIABLE, {name: v});
    }

String
    = '"' str:(StringItem*) '"' {
        if (str.length === 1 && typeof str[0] === 'string') {
            return str[0];
        } else {
            return mk(T.INTERPOLATED_STRING, {chunks: str});
        }
    }

StringItem
    = $( StringChar+ )
    / TemplateExpansion

StringChar
    = '\\n'     { return "\n"; }
    / '\\t'     { return "\t"; }
    / '\\"'     { return '"'; }
    / '\\\\'    { return "\\"; }
    / '{' !'{'  { return String.fromCharCode(123); /* workaround for pegjs bug */ }
    / [^\{\"]

Symbol
    = sym:$( SymbolAtom+ ) !':' {
        return mk(T.SYMBOL, {name: sym});
    }

SymbolAtom
    = SymbolChar / ':' &SymbolChar

SymbolChar
    = [a-z0-9\.\\/_-]i

Array
    = '[' __ items:AtomList? __ ']' {
        return mk(T.ARRAY_LITERAL, {items: items || []});
    }

AtomList
    = head:Atom _ tail:(',' __ Atom)* {
        var out = [head];
        for (var i = 0; i < tail.length; ++i) {
            out.push(tail[i][2]);
        }
        return out;
    }

Dictionary
    = '{' __ items:DictionaryPairs? __ '}' {
        return mk(T.DICTIONARY_LITERAL, {dict: items});
    }

DictionaryPairs
    = head:DictionaryPair _ tail:(',' __ DictionaryPair)* {
        var out = {};
        out[head[0]] = head[1];
        tail.forEach(function(t) {
            out[t[2][0]] = t[2][1];
        });
        return out;
    }

DictionaryPair
    = key:key _ value:Atom {
        return [key, value];
    }

//
// Helpers

_
    = [ \t]*

__
    = IGNORE*

IGNORE
    = [ \t\r\n]+
    / COMMENT

NL
    = '\n'
    / '\r' '\n'?

COMMENT
    = '#' [^\r\n]*

TERM
    = _ &EndKeyword
    / _ COMMENT? !.
    / _ COMMENT? NL __

EXPANSION_START
    = '{{'

EXPANSION_END
    = '}}'

ident_start
    = [a-z]i

ident_rest
    = [a-z0-9_-]i

ident
    = !Keyword name:$( ident_start ident_rest* ) { return name; }

key
    = name:$( ident_start ident_rest* ) ':' {
        return name;
    }

simple_variable
    = '$' name:variable_name {
        return name;
    }

variable_name
    = $( [a-zA-Z_]i [a-zA-Z0-9_]* )

member
    = static_member

static_member
    = '.' name:$( ident_start ident_rest* ) {
        return { t: 'static', name: name };
    }

//
// Keywords

Keyword
    = IfKeyword
    / InKeyword
    / ThenKeyword
    / ElseKeyword
    / EndKeyword
    / ForeachKeyword

IfKeyword       = 'if'      !ident_rest
InKeyword       = 'in'      !ident_rest
ThenKeyword     = 'then'    !ident_rest
ElseKeyword     = 'else'    !ident_rest
EndKeyword      = 'end'     !ident_rest
ForeachKeyword  = 'foreach' !ident_rest